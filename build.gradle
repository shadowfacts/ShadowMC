buildscript {
	repositories {
		jcenter()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
	}
	dependencies {
		classpath "net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT"
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlin_version}"
	}
}

plugins {
	id "maven"
	id "com.github.johnrengelman.shadow" version "1.2.3"
	id "com.matthewprenger.cursegradle" version "1.0.6"
}

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "kotlin"

version = minecraft_version + "-" + mod_version

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

minecraft {
	version = minecraft_version + "-" + forge_version
	runDir = "run"

	mappings = mcp_mappings

	replaceIn "ShadowMC.java"
	replace '@VERSION@', mod_version
}

processResources {
	inputs.property "version", project.version
	inputs.property "mcversion", minecraft_version

	from (sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'
		expand 'version':project.version, 'mcversion':minecraft_version
	}
	from (sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
}

repositories {
	maven {
		name "shadowfacts"
		url "http://mvn.rx14.co.uk/shadowfacts/"
	}
}

dependencies {
	compile group: "net.shadowfacts", name: "ShadowLib", version: shadowlib_version
	compile group: "net.shadowfacts", name: "ShadowConfig", version: shadowconfig_version
	compile group: "net.shadowfacts", name: "Mirror", version: mirror_version

	compile group: "org.projectlombok", name: "lombok", version: lombok_version

	compile group: "org.jetbrains.kotlin", name: "kotlin-stdlib", version: kotlin_version
	compile group: "org.jetbrains.kotlin", name: "kotlin-reflect", version: kotlin_version
	compile group: "org.jetbrains.kotlin", name: "kotlin-runtime", version: kotlin_version
}

shadowJar {
	classifier = ""
	dependencies {
		include(dependency("net.shadowfacts:ShadowLib:${shadowlib_version}"))
		include(dependency("net.shadowfacts:ShadowConfig:${shadowconfig_version}"))
		include(dependency("net.shadowfacts:Mirror:${mirror_version}"))

		include(dependency("org.jetbrains.kotlin:kotlin-stdlib:${kotlin_version}"))
		include(dependency("org.jetbrains.kotlin:kotlin-reflect:${kotlin_version}"))
		include(dependency("org.jetbrains.kotlin:kotlin-runtime:${kotlin_version}"))
	}
	exclude "test/*"
}

tasks.build.dependsOn sourceJar, shadowJar

artifacts {
	archives shadowJar
	archives sourceJar
}

curseforge {
	apiKey = project.hasProperty('curseForgeApiKey') ? project.curseForgeApiKey : ''

	project {
		id = "226780"

		addGameVersion "1.10"
		addGameVersion "Java 8"

		mainArtifact(shadowJar) {
			releaseType = "beta"
			changelog = file("changelog.txt").text
		}
	}
}

reobf { shadowJar { mappingType = 'SEARGE' } }
tasks.reobfShadowJar.mustRunAfter shadowJar
afterEvaluate {
	tasks.curseforge226780.dependsOn reobfShadowJar
}
